PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?tool (SAMPLE(?label_final) AS ?label) (SAMPLE(?description_final) AS ?description) ?date_modified (GROUP_CONCAT(DISTINCT ?tadirahIRI; SEPARATOR=", ") AS ?tadirahIRIs) WHERE {
  ?concept wdt:P9309 ?tadirahId .
  ?tool wdt:P366 ?concept ;
        wdt:P31/wdt:P279* wd:Q7397 ;
        ^schema:about/schema:dateModified ?date_modified .
  OPTIONAL {
    ?tool rdfs:label ?label_en FILTER (LANG(?label_en) = "en")
  } .
  OPTIONAL {
    ?tool schema:description ?description_en FILTER (LANG(?description_en) = "en")
  } .
  # Fallback: extrahiere Q-ID aus URI  
  BIND (STRAFTER(STR(?tool),"http://www.wikidata.org/entity/") AS ?qid)
  # label_final = englisches Label, sonst Q-ID
  BIND (COALESCE(?label_en, ?qid) AS ?label_final)
  # description_final = englische Beschreibung, sonst leer
  BIND (COALESCE(?description_en, '') AS ?description_final)
  BIND (IRI(CONCAT("https://vocabs.dariah.eu/tadirah/", STR(?tadirahId))) AS ?tadirahIRI)
}
GROUP BY ?tool ?date_modified
ORDER BY ASC(?label)
LIMIT 10000

