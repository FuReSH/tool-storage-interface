PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX schema: <http://schema.org/>

SELECT ?toolID 
  ?toolLabel 
  ?toolDesc
  ?copyrightLabel 
  ?licenseLabel 
  (GROUP_CONCAT(DISTINCT ?image; separator = ", ") AS ?images) 
  (GROUP_CONCAT(DISTINCT ?sourceRepo; separator = ", ") AS ?sourceRepos) 
  (GROUP_CONCAT(DISTINCT ?website; separator = ", ") AS ?websites) 
  (GROUP_CONCAT(DISTINCT ?instanceOfLabel; separator = ", ") AS ?instanceOfLabels) 
  (GROUP_CONCAT(DISTINCT ?tadirahID; separator = ", ") AS ?tadirahIDs)
  (GROUP_CONCAT(DISTINCT ?tadirahLabel; separator = ", ") AS ?tadirahLabels) 
WHERE {
  # Query Wikidata for tools and their Tadirah IDs
  SERVICE <https://query.wikidata.org/sparql> {
    ?method wdt:P9309 ?tadirahIDString .
  # select all items which are linked to these methods through has use
  ?tool wdt:P366 ?method;
    # limit tools to software in the broadest sense
    wdt:P31/wdt:P279* wd:Q7397.
  # retrieve properties that are part of our data model
  ?tool wdt:P31 ?instanceOf.
  OPTIONAL { ?tool  wdt:P6216 ?copyright;
    wdt:P275 ?license;
    wdt:P1324 ?sourceRepo;
    wdt:P856 ?website .}
  # retrieve the logos and images for those tools that have them
  OPTIONAL { ?tool wdt:P154 ?logo. }
  OPTIONAL { ?tool wdt:P18 ?pic. }
  BIND(COALESCE(?logo, ?pic) AS ?image).
  BIND(STR(?tool) AS ?toolID) .
  BIND(IRI(CONCAT("https://vocabs.dariah.eu/tadirah/", ?tadirahIDString)) AS ?tadirahID)
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en".
    ?tool rdfs:label ?toolLabel;
      schema:description ?toolDesc.
    ?instanceOf rdfs:label ?instanceOfLabel.
          ?copyright rdfs:label ?copyrightLabel.
          ?license rdfs:label ?licenseLabel.
  }
  }
  
  # Query Tadirah ontology for prefLabel using the Tadirah ID
  ?tadirahID skos:prefLabel ?tadirahLabel .
  FILTER (lang(?tadirahLabel) = "en")
}
GROUP BY ?toolID ?toolLabel ?toolDesc ?copyrightLabel ?licenseLabel
ORDER BY ?toolLabel
LIMIT 5000